package Analyzer;

import java_cup.runtime.*;
import java.util.LinkedList;
import AST.*;
import AST.Expresiones.*;
import AST.Sentencias.*;

parser code
{:
    /* RAIZ */
    Principal principal;

    public void setPrincipal(Principal principal){
        this.principal = principal;
    }

    /**
    *   Metodo que retorna la raiz del arbol
    **/
    public Principal getPrincipal(){
        return this.principal;
    }
    
    /**
     * Método al que se llama automáticamente ante algún error sintactico.
     **/
    public void syntax_error(Symbol s){
        System.out.println("Error sintactico en la linea: " + (s.left) +" Columna: " +s.right+ ". No se esperaba este componente: " + s.value+"."); 
        Token token = new Token();
        token.lexema = s.value+"";
        token.componenteLexico = "Sintáctico";
        token.descripcion = "No se esperaba: "+s.value;
        token.columna = s.right+1+"";
        token.fila = s.left+1+"";
        this.principal.addError(token);
    }
    /**
     * Método al que se llama en el momento en que ya no es posible una recuperación de errores.
     **/
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.out.println("Error Sintactico irrecuperable en la Lienea " + (s.left)+ " Columna "+s.right+". Componente " + s.value + " no reconocido."); 
        Token token = new Token();
        token.lexema = s.value+"";
        token.componenteLexico = "Sintáctico inrecuperable";
        token.descripcion = "No se esperaba: "+s.value;
        token.columna = s.right+1+"";
        token.fila = s.left+1+"";
        this.principal.addError(token);
    }
:}

terminal String res_null, res_false, res_true,

res_function, res_in, 

res_if, res_else, res_while, res_for, res_switch, res_case, res_default, res_break, res_continue, res_return, res_do,

l_corchete, r_corchete, l_llave, r_llave, l_parent, r_parent,
coma, puntocoma, igualigual, igual, mas, menos, por, potencia, modular, div, ternario, mayorigual, mayor, menorigual, menor, diferente,
and, or, not, dospuntos,

numero, doble, id, cadena,

UMINUS,UPLUS
;

non terminal S
;

non terminal PUEDE_SEMICOLON
;

non terminal LinkedList<Expresion> LISTA_E, LISTA_LLAMADA, PARAMETROS_LLAMADA;
non terminal LinkedList<Nodo> LISTA_BLOQUES, BLOQUES, DECLARACIONES;
non terminal Nodo DECLARACION, INSTRUCCION;
non terminal Expresion CONSTANTE, E, LLAMADA, BINARIA, UNARIA, ARITMETICA, LOGICA, RELACIONAL, TERNARIA;
non terminal Sentencia SENTENCIA, FUNCION, VARIABLE, IF, ELSE, WHILE, DOWHILE, FOR, SWITCH, RETURN;
non terminal Default DEFAULT;
non terminal Parametro PARAMETRO;
non terminal LinkedList<Caso> LISTA_CASOS;
non terminal LinkedList<Parametro> LISTA_PARAMETROS, PARAMETROS;

precedence left or;
precedence left and;
precedence left diferente,igualigual;
precedence nonassoc mayor,mayorigual,menor,menorigual;
precedence left mas,menos;
precedence left por,div,modular;
precedence left potencia;
precedence right not,UMINUS,UPLUS;
precedence left ternario;
precedence left l_parent,r_parent,l_corchete,r_corchete;

start with S;

S ::= DECLARACIONES:a
    {:
        principal.setNodos(a);
    :}
;

DECLARACIONES::= DECLARACIONES:a DECLARACION:b
    {:
        RESULT = a;
        a.add(b);
    :}
    | DECLARACION:a
    {:
        RESULT = new LinkedList<Nodo>();
        RESULT.add(a);
    :}
;

DECLARACION ::= FUNCION:a
    {:
        RESULT = a;
    :}
    | SENTENCIA:a
    {:
        RESULT = a;
    :}
    | INSTRUCCION:a PUEDE_SEMICOLON
    {:
        RESULT = a;
    :}
;

VARIABLE ::= id:a igual E:b 
    {:
        RESULT = new Declaracion(a,b,aleft+1,aright+1);
    :}
;

FUNCION ::= id:a igual res_function l_parent PARAMETROS:b r_parent l_llave BLOQUES:c r_llave
    {:
        RESULT = new Funcion(a,b,c,aleft+1,aright+1);
    :}
    | id:a igual l_parent PARAMETROS:b r_parent igual mayor l_llave BLOQUES:c r_llave
    {:
        RESULT = new Funcion(a,b,c,aleft+1,aright+1);
    :}
;

PARAMETROS ::= LISTA_PARAMETROS:a
    {:
        RESULT = a;
    :}
    | /* empty */
    {:
        RESULT = new LinkedList<Parametro>();
    :}
;

LISTA_PARAMETROS ::= LISTA_PARAMETROS:a coma PARAMETRO:b
    {:
        RESULT = a;
        RESULT.add(b);
    :}
    | PARAMETRO:a
    {:
        RESULT = new LinkedList<Parametro>();
        RESULT.add(a);
    :}
;

PARAMETRO ::= E:a 
    {:
        RESULT = new Parametro(null,a,a.fila,a.columna);
    :}
    | id:a igual E:b
    {:
        RESULT = new Parametro(a,b,aleft+1,aright+1);
    :}
;

BLOQUES ::= LISTA_BLOQUES:a
    {:
        RESULT = a;
    :}
    | /* empty */
    {:
        RESULT = new LinkedList<Nodo>();
    :}
;

LISTA_BLOQUES ::= LISTA_BLOQUES:a INSTRUCCION:b PUEDE_SEMICOLON
    {:
        RESULT = a;
        RESULT.add(b);
    :}
    | LISTA_BLOQUES:a SENTENCIA:b
    {:
        RESULT = a;
        RESULT.add(b);
    :}
    | SENTENCIA:a 
    {:
        RESULT = new LinkedList<Nodo>();
        RESULT.add(a);
    :}
    | INSTRUCCION:a PUEDE_SEMICOLON
    {:
        RESULT = new LinkedList<Nodo>();
        RESULT.add(a);
    :}
;

SENTENCIA ::= IF:a
    {:
        RESULT = a;
    :}
    | SWITCH:a
    {:
        RESULT = a;
    :}
    | WHILE:a
    {:
        RESULT = a;
    :}
    | DOWHILE:a
    {:
        RESULT = a;
    :}
    | FOR:a
    {:
        RESULT = a;
    :}
;

INSTRUCCION ::= res_break:a
    {:
        RESULT = new Corte(Corte.TIPO_CORTE.BREAK,aleft+1,aright+1);
    :}
    | res_continue:a
    {:
        RESULT = new Corte(Corte.TIPO_CORTE.CONTINUE,aleft+1,aright+1);
    :}
    | LLAMADA:a
    {:
        RESULT = a;
    :}
    | VARIABLE:a
    {:
        RESULT = a;
    :}
    | RETURN:a
    {:
        RESULT = a;
    :}
;

RETURN ::= res_return:b l_parent E:a r_parent
    {:
        RESULT = new Return(a,bleft+1,bright+1);
    :}
    | res_return:b
    {:
        RESULT = new Return(null,bleft+1,bright+1);
    :}
;

IF ::= res_if:c l_parent E:a r_parent l_llave BLOQUES:b r_llave
    {:
        RESULT = new If(a,b,null,cleft+1,cright+1);
    :}
    | res_if:c l_parent E:a r_parent l_llave BLOQUES:b r_llave ELSE:d
    {:
        RESULT = new If(a,b,d,cleft+1,cright+1);
    :}
;

ELSE ::= res_else:c IF:a
    {:
        RESULT = a;
    :}
    | res_else:c l_llave BLOQUES:a r_llave
    {:
        RESULT = new Else(a,cleft+1,cright+1);
    :}
;

WHILE ::= res_while:c l_parent E:a r_parent l_llave BLOQUES:b r_llave
    {:
        RESULT = new While(a,b,cleft+1,cright+1);
    :}
;

DOWHILE ::= res_do:c l_llave BLOQUES:b r_llave res_while l_parent E:a r_parent puntocoma
    {:
        RESULT = new DoWhile(a,b,cleft+1,cright+1);
    :}
;

FOR ::= res_for l_parent id:a res_in E:b r_parent l_llave BLOQUES:c r_llave
    {:
        RESULT = new For(a,b,c,aleft+1,aright+1);
    :}
;

SWITCH ::= res_switch:d l_parent E:a r_parent l_llave LISTA_CASOS:b DEFAULT:c r_llave
    {:
        RESULT = new Switch(a,b,c,dleft+1,dright+1);
    :}
;

LISTA_CASOS ::= LISTA_CASOS:a res_case:d E:b dospuntos BLOQUES:c
    {:
        RESULT = a;
        RESULT.add(new Caso(b,c,dleft+1,dright+1));
    :}
    | res_case:c E:a dospuntos BLOQUES:b
    {:
        RESULT = new LinkedList<Caso>();
        RESULT.add(new Caso(a,b,cleft+1,cright+1));
    :}
;

DEFAULT ::= res_default:a dospuntos BLOQUES:b
    {:
        RESULT = new Default(b,aleft+1,aright+1);
    :}
    | /*empty*/
    {:
        RESULT = null;
    :}
;

PARAMETROS_LLAMADA ::= LISTA_LLAMADA:a
    {:
        RESULT = a;
    :}
    | /* empty */
    {:
        RESULT = new LinkedList<Expresion>();
    :}
;

LISTA_LLAMADA ::= LISTA_LLAMADA:a coma E:b
    {:
        RESULT = a;
        RESULT.add(b);
    :}
    | LISTA_LLAMADA:a coma res_default:b
    {:
        RESULT = a;
        RESULT.add(new Primitivo(Expresion.TIPO_PRIMITIVO.DEFAULT,Expresion.TIPO_PRIMITIVO.DEFAULT,bleft+1,bright+1));
    :}
    | res_default:a
    {:
        RESULT = new LinkedList<Expresion>();
        RESULT.add(new Primitivo(Expresion.TIPO_PRIMITIVO.DEFAULT,Expresion.TIPO_PRIMITIVO.DEFAULT,aleft+1,aright+1));
    :}
    | E:a
    {:
        RESULT = new LinkedList<Expresion>();
        RESULT.add(a);
    :}
;

LISTA_E ::= LISTA_E:a coma E:b
    {:
        RESULT = a;
        RESULT.add(b);
    :}
    | E:a
    {:
        RESULT = new LinkedList<Expresion>();
        RESULT.add(a);
    :}
;

E ::= CONSTANTE:a    
    {:
        RESULT = a;
    :}
    | BINARIA:a
    {:
        RESULT = a;
    :}
    | UNARIA:a
    {:
        RESULT = a;
    :}
    | TERNARIA:a 
    {:
        RESULT = a;
    :}
    | LLAMADA:a
    {:
        RESULT = a;
    :}
    | l_parent E:a r_parent
    {:
        RESULT = a;
    :}
    /*
    | DECLARACION_ARREGLO:a
    {:
    :}
    */
;

TERNARIA ::= E:a ternario:d E:b dospuntos E:c 
{:
    RESULT = new Ternaria(a,b,c,dleft+1,dright+1);
:}
;

LLAMADA ::= id:a l_parent PARAMETROS_LLAMADA:b r_parent
    {:
        RESULT = new Llamada(a,b,aleft+1,aright+1);
    :}
;

CONSTANTE ::= numero:a
    {:
        RESULT = new Primitivo(Integer.parseInt(a),Expresion.TIPO_PRIMITIVO.INTEGER,aleft+1,aright+1);
    :}
    | res_true:a
    {:
        RESULT = new Primitivo(true,Expresion.TIPO_PRIMITIVO.BOOLEAN,aleft+1,aright+1);
    :}
    | res_false:a
    {:
        RESULT = new Primitivo(false,Expresion.TIPO_PRIMITIVO.BOOLEAN,aleft+1,aright+1);
    :}
    | doble:a
    {:
        RESULT = new Primitivo(Double.parseDouble(a),Expresion.TIPO_PRIMITIVO.DOUBLE,aleft+1,aright+1);
    :}
    | res_null:a
    {:
        RESULT = new Primitivo("",Expresion.TIPO_PRIMITIVO.STRING,aleft+1,aright+1);
    :}
    | cadena:a
    {:
        String cad = a.substring(1,a.length());
        cad = cad.substring(0,cad.length()-1);
        RESULT = new Primitivo(cad,Expresion.TIPO_PRIMITIVO.STRING,aleft+1,aright+1);
    :}
    | id:a
    {:
        RESULT = new Primitivo(a,Expresion.TIPO_PRIMITIVO.VARIABLE,aleft+1,aright+1);
    :}
;

BINARIA ::= ARITMETICA:a
    {:
        RESULT = a;
    :}
    | LOGICA:a
    {:
        RESULT = a;
    :}
    | RELACIONAL:a
    {:
        RESULT = a;
    :}
;

UNARIA::= menos:b E:a
    {:
        RESULT = new Unaria(a,Unaria.TIPO_OPERACION.MENOS,bleft+1,bright+1);
    :}
    %prec UMINUS
    | not:b E:a
    {:
        RESULT = new Unaria(a,Unaria.TIPO_OPERACION.NOT,bleft+1,bright+1);
    :}
    | mas:b E:a
    {:
        RESULT = a;
    :}
    %prec UPLUS
;

ARITMETICA ::= E:a mas:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.MAS,cleft+1,cright+1);
    :}
    | E:a menos:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.MENOS,cleft+1,cright+1);
    :}
    | E:a por:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.POR,cleft+1,cright+1);
    :}
    | E:a div:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.DIV,cleft+1,cright+1);
    :}
    | E:a potencia:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.POT,cleft+1,cright+1);
    :}
    | E:a modular:c E:b
    {:
        RESULT = new Aritmetica(a,b,Aritmetica.TIPO_OPERACION.MOD,cleft+1,cright+1);
    :}
;

LOGICA ::= E:a or:c E:b
    {:
        RESULT = new Logica(a,b,Logica.TIPO_OPERACION.OR,cleft+1,cright+1);
    :}
    | E:a and:c E:b
    {:
        RESULT = new Logica(a,b,Logica.TIPO_OPERACION.AND,cleft+1,cright+1);
    :}
;

RELACIONAL ::= E:a menor:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.MENOR,cleft+1,cright+1);
    :}
    | E:a menorigual:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.MENORIGUAL,cleft+1,cright+1);
    :}
    | E:a mayor:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.MAYOR,cleft+1,cright+1);
    :}
    | E:a mayorigual:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.MAYORIGUAL,cleft+1,cright+1);
    :}
    | E:a igualigual:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.IGUALIGUAL,cleft+1,cright+1);
    :}
    | E:a diferente:c E:b
    {:
        RESULT = new Relacional(a,b,Relacional.TIPO_OPERACION.DIFERENTE,cleft+1,cright+1);
    :}
;

PUEDE_SEMICOLON ::= puntocoma
    | /*empty*/
;